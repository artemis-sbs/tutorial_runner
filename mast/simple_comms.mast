
#
# Show the proper GUI for server vs Client Consoles
#

# Configure the label where comms routing occurs

route comms select comms_route

route grid select damcon_route

route change console fred

if IS_SERVER:
    start_text = "This is a start project for mast"
    ->server_main
else:
    -> client_main
end_if




======= server_main =====

#
# Create the player ship so the clients see it
# This is a simple script that has one playable ship
#
simulation create
delay gui 1s
shared artemis =  to_id(player_spawn(0,0,0, "Artemis", "tsn", "tsn_battle_cruiser"))
do set_face(artemis, random_terran())
do assign_client_to_ship(0,artemis)
go1 = grid_spawn(artemis, "fred", "fred", 9,4, 3, "blue", "flint")
go2 = grid_spawn(artemis, "barney", "barney", 8,4, 3, "green", "rubble")
do set_face(go1.id, random_terran())
do set_face(go2.id, random_terran())


section style="area:2,20,80,35;"

""" {start_text}"""

await choice:
    + "Start":
        schedule start
end_await
### This continues on to the next label

===== end_game =======
# 
# Check for end game
#
# no more players
players = role("__PLAYER__")
if len(players) == 0:
    start_text = "You lost"
    simulation pause
    -> server_main
end_if
#
# No more enemies
#
raiders = role("raider")
if len(raiders) == 0:
    start_text = "You won"
    simulation pause
    -> server_main
end_if
delay sim 5s
-> end_game

======== start ======
simulation resume
# Create the world here

# Create a space station
ds1 = npc_spawn(1000,0,1000, "DS1", "tsn", "starbase_command", "behav_station")
ds2 = npc_spawn(1000,0,-1000, "DS2", "tsn", "starbase_command", "behav_station")
do add_role({ds1.id, ds2.id}, "Station")
do set_face(ds1.id, random_terran())
do set_face(ds2.id, random_terran())

# Create an enemy
k001 = npc_spawn(-1000,0,1000, "K001", "raider", "kralien_dreadnaught", "behav_npcship")
do set_face(k001.id, random_kralien())

->END


===== fred ====

====== client_main =======

#
# Default the console to helm
#
console_select = "helm"

====== select_console ======

section style="area:2,20,80,35;"

"""Select your console"""

section style="area: 85,50, 99,90;"
vradio console_select "helm,weapons, comms,science,engineering"
blank
row

button "accept":
    ->console_selected
end_button

await gui
->END

====== console_selected ====
do assign_client_to_ship(client_id,artemis)
console console_select
await gui


================ comms_route ==================
#
# This label is called for a player ship (COMMS_ORIGIN_ID)
# and the COMMS_SELECTED_ID ship has not been communicated with
# this is used to resolve where to START the conversation with the TO ship
#
# COMMS_SELECTED_ID is the id of the target

if COMMS_SELECTED_ID == COMMS_ORIGIN_ID:
    # This is the same ship
    jump internal_comms
elif has_role(COMMS_SELECTED_ID, 'Station'):
    jump comms_station
elif has_role(COMMS_SELECTED_ID, 'raider'):
     jump npc_comms
end_if

# Anything else has no comms buttons
# and static as the id
comms_info "static"

->END

================ internal_comms ==================
#
# Setup faces for the departments
#
doctor = random_terran()
biologist = random_terran()
counselor = random_terran()
major = random_terran()
sec = "Security"

================ internal_comms_loop ==================
#
# Shows button color, face and title overrides
#
await comms:
    + "Sickbay" color "blue":
        receive "The crew health is great!" title "sickbay" face "{doctor}" color "blue"
    + "Security" color "red":
        receive  "All secure" title sec face major color "red"
    + "Exobiology" color "green":
        receive  "Testing running, one moment" title "Exobiology" face biologist color "green"
        # It is best to schedule delayed responses so the comms buttons are not stalled
        schedule test_finished
    + "counselor" color "cyan":
        receive  "Something is disturbing the crew" title "counselor" face counselor color "cyan"
        #
        # but you can delay comms, There will be no buttons during this delay
        #
        delay sim 3s
        receive  "Things feel like they are getting worse" title "counselor" face counselor color "cyan"
end_await
-> internal_comms_loop

====== test_finished ======
# This is a new task to delay the response of the test results
# COMMS variables should migrate to this task

delay sim 2s
receive  "test results finished. Tell the captain we have a pathogen. This could be bad." title "Exobiology" face biologist color "green"
->END



================ npc_comms ==================

await comms:
    + "Hail":
        receive "We will destroy you, disgusting Terran scum!"
    + "Surrender now":
        receive  """OK we give up"""
end_await
-> npc_comms


======== comms_station ====== 
comms_id = "later"

await comms:
    + "Hail":
        transmit "Hello"
        receive "Yo"
end_await
-> comms_station


================ damcon_route ==================

# COMMS_SELECTED_ID is the id of the target

if has_role(COMMS_SELECTED_ID, 'flint'):
    jump comms_flintstone
elif has_role(COMMS_SELECTED_ID, 'rubble'):
     jump comms_rubble
end_if
->END

================ comms_flintstone ==================

await comms:
    + "Hail":
        have client_id broadcast "Yabba Daba Dooo"

end_await
-> comms_flintstone

================ comms_rubble ==================

await comms:
    + "Hail":
        have client_id broadcast "Who ya doing fred?"

end_await
-> comms_rubble
