
#
# Show the proper GUI for server vs Client Consoles
#

if IS_SERVER:
    start_text = "This is a start project for mast"
    ->server_main
else:
    -> client_main
end_if


======= server_main =====

#
# Create the player ship so the clients see it
# This is a simple script that has one playable ship
#
simulation create
delay gui 1s
shared artemis =  to_id(player_spawn(0,0,0, "Artemis", "tsn", "tsn_battle_cruiser"))
do assign_client_to_ship(0,artemis)

section style="area:2,20,80,35;"

""" {start_text}"""

await choice:
    + "Start":
        ->> start
end_await
### This continues on to the next label

===== end_game =======
# 
# Check for end game
#
# no more players
players = role("__PLAYER__")
if len(players) == 0:
    start_text = "You lost"
    simulation pause
    -> server_main
end_if
#
# No more enemies
#
raiders = role("raider")
if len(raiders) == 0:
    start_text = "You won"
    simulation pause
    -> server_main
end_if
delay sim 5s
-> end_game

======== start ======
simulation resume
# Create the world here

# Create a space station
ds1 = npc_spawn(1000,0,1000, "DS1", "tsn", "starbase_command", "behav_station")
ds2 = npc_spawn(1000,0,-1000, "DS2", "tsn", "starbase_command", "behav_station")
do add_role({ds1.id, ds2.id}, "Station")

# Create an enemy
k001 = npc_spawn(-1000,0,1000, "K001", "raider", "kralien_dreadnaught", "behav_npcship")

schedule task_npc_targeting

<<-


====== client_main =======

#
# This handles the change client button to return to the select_console
#
event change_console:
    ->select_console
end_event
#
# Default the console to helm
#
console_select = "helm"

====== select_console ======

section style="area:2,20,80,35;"

"""Select your console"""

section style="area: 85,50, 99,90;"
vradio console_select "helm,weapons, comms,science,engineering"
blank
row

button "accept":
    ->console_selected
end_button

await gui
->END

====== console_selected ====
do assign_client_to_ship(client_id,artemis)
console console_select
await gui

========== task_npc_targeting =========
raiders = role('raider')
if len(raiders)==0:
    ->END
end_if

for raider in raiders:
    the_target = closest(raider, role("__PLAYER__"), 2000)
    if the_target is None:
        the_target = closest(raider, role("Station"))
    end_if
    if the_target is not None:
        do target(sim, raider, the_target, True)
    end_if
next raider

delay sim 5s
jump task_npc_targeting