#
# Show the proper GUI for server vs Client Consoles
#
# uncomment to see log messages
#logger

#
# A good place to setup AI is in the Spawn task
# A spawn task is run soon after the object is spawned
#
route spawn route_ai
#
# Show that multiple routes can be made
route spawn route_station

if IS_SERVER:
    start_text = "This is a start project for mast"
    ->server_main
else:
    -> client_main
end_if


======= server_main =====


#
# Create the player ship so the clients see it
# This is a simple script that has one playable ship
#
simulation create
delay gui 1s
shared artemis =  to_id(player_spawn(0,0,0, "Artemis", "tsn", "tsn_battle_cruiser"))
do assign_client_to_ship(0,artemis)

section style="area:2,20,80,35;"

""" {start_text}"""

await choice:
    + "Start":
        -> start
    end_await
-> server_main


======= win_server_main =====

section style="area:2,20,80,35;"

""" WINNER """

await choice:
    + "restart":
        -> server_main
end_await

======= lose_server_main =====

section style="area:2,20,80,35;"

""" LOSER """

await choice:
    + "restart":
        ->server_main
end_await


===== end_game =======
# 
# Check for end game
#
# no more players
players = role("__PLAYER__")
if len(players) == 0:
    start_text = "You lost"
    simulation pause
    reroute server lose_server_main
    reroute clients select_console
    ->END
end_if
#
# No more enemies
#
raiders = role("raider")
if len(raiders) == 0:
    start_text = "You won"
    simulation pause
    reroute server win_server_main
    reroute clients select_console
    ->END
end_if
delay sim 5s
-> end_game

======== start ======

# Create the world here

# Create a space station
ds1 = npc_spawn(1000,0,1000, "DS1", "tsn", "starbase_command", "behav_station")
ds2 = npc_spawn(1000,0,-1000, "DS2", "tsn", "starbase_command", "behav_station")
do add_role({ds1.id, ds2.id}, "Station")

# Create an enemy
k001 = npc_spawn(-1000,0,1000, "K001", "raider", "kralien_dreadnaught", "behav_npcship")
simulation resume
schedule end_game
delay gui 99m


====== client_main =======

#
# This handles the change client button to return to the select_console
#
event change_console:
    ->select_console
end_event
#
# Default the console to helm
#
console_select = "helm"

====== select_console ======

section style="area:2,20,80,35;"

"""Select your console"""

section style="area: 85,50, 99,90;"
vradio console_select "helm,weapons, comms,science,engineering"
blank
row

button "accept":
    ->console_selected
end_button

await gui
->END

====== console_selected ====
do assign_client_to_ship(client_id,artemis)
console console_select
await gui

========== route_ai =========
#
# SPAWNED_ID is a special value of the ID of the thing spawned
#
if has_role(SPAWNED_ID, "raider"):
    jump npc_targeting_ai
end_if
#if not a raider end the task
->END


=====  npc_targeting_ai   =========

the_target = closest(SPAWNED_ID, role("__PLAYER__"), 2000)
if the_target is None:
    the_target = closest(SPAWNED_ID, role("Station"))
end_if
if the_target is not None:
    do target(sim, SPAWNED_ID, the_target, True)
end_if

delay sim 5s
jump npc_targeting_ai


========== route_station =========

# Just an example to show multiple spawn routes

if has_role(SPAWNED_ID, "station"):
    log "Station"
end_if
->END
