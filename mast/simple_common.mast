#
# Show the proper GUI for server vs Client Consoles
#
# uncomment to see log messages
#logger

# This handles the change client button to return to the select_console
#
route_change_console("select_console")
reroute server server_main
reroute clients client_main
enemy_count = 1
start_text = "This is a start project for mast"


======= server_main =====
#
# Create the player ship so the clients see it
# This is a simple script that has one playable ship
#
sim_create()
#delay gui 1s
shared artemis =  to_id(player_spawn(0,0,0, "Artemis", "tsn", "tsn_battle_cruiser"))

set_face(artemis, random_terran())
assign_client_to_ship(0,artemis)
go1 = grid_spawn(artemis, "fred", "fred", 9,4, 3, "blue", "flint")
go2 = grid_spawn(artemis, "barney", "barney", 8,4, 3, "green", "rubble")
set_face(go1.id, random_terran())
set_face(go2.id, random_terran())

gui_section(style="area:2,20,80,35;")

""" {start_text}"""

await choice:
    + "Start":
        -> start
    end_await
-> server_main


======= win_server_main =====

gui_section(style="area:2,20,80,35;")

""" WINNER """

await choice:
    + "restart":
        -> server_main
end_await

======= lose_server_main =====

gui_section(style="area:2,20,80,35;")

""" LOSER """

await choice:
    + "restart":
        ->server_main
end_await


===== end_game =======
# 
# Check for end game
#
# no more players
players = role("__PLAYER__")
if len(players) == 0:
    start_text = "You lost"
    sim_pause()
    reroute server lose_server_main
    reroute clients select_console
    ->END
end_if
#
# No more enemies
#
raiders = role("raider")
if len(raiders) == 0:
    start_text = "You won"
    sim_pause()
    reroute server win_server_main
    reroute clients select_console
    ->END
end_if
delay sim 5s
-> end_game

======== start ======

# Create the world here

# Create a space station
ds1 = npc_spawn(1000,0,1000, "DS1", "tsn, Station", "starbase_command", "behav_station")
ds2 = npc_spawn(1000,0,-1000, "DS2", "tsn, Station", "starbase_command", "behav_station")
set_face(ds1.id, random_terran())
set_face(ds2.id, random_terran())


# Create an enemy
for x in range(enemy_count):
    k001 = npc_spawn(-1000+x*250,0,1000, f"K00{x}", "raider", "kralien_battleship", "behav_npcship")
    set_face(k001.id, random_face("kralien_battleship"))
next x

sim_resume()
schedule end_game

==== additional_start ===
await gui


====== client_main =======

#
# Default the console to helm
#
console_select = "helm"

====== select_console ======

gui_section(style="area:2,20,80,35;")

"""Select your console"""

gui_section(style="area: 85,50, 99,90;")
gui_vradio("helm,weapons, comms,science,engineering,mainscreen", var="console_select")
gui_row("row-height:40px;")

on message gui_button("accept"):
    ->console_selected
end_on

await gui
->END

====== console_selected ====
assign_client_to_ship(client_id,artemis)
gui_console(console_select)
#
# Marking with a label so it can be overridden
# see reuse.mast
#
======= console_bottom_choices ====
await gui

