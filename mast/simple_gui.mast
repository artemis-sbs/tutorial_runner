
#
# Show the proper GUI for server vs Client Consoles
#
shared skills = ~~{
        "sensors": {
            "description": "This is the stuff that ",
            "tier": [ {
                "skills": [
                    {
                        "icon": 10,
                        "name": "Something",
                        "description": "This is some sensor skill",
                        "rank": [
                            {
                                "text": "Do this 5 times",
                                "description": "You did somethings that get you to a rank 1",
                                "goal": 5
                            },
                            {
                                "text": "Do another thing 50 times",
                                "description": "You did somethings that get you to a rank 2",
                                "goal": 50
                            }, 
                            {
                                "text": "Do another thing 10 times",
                                "description": "You did somethings that get you to a rank 3",
                                "goal": 10
                            
                            },
                            {
                                "text": "Do another thing 1 time",
                                "description": "You did somethings that get you to a rank 4",
                                "goal": 1
                            }
                        ]
                    }
                ]

            }]
            
        },
        "weapons": {
            "icons": [2]
        },
        "engines": {
            "icons": [3]
        },
       "shields": {
            "icons": [3]
        }
}~~
tab = "sensors"

=========start=======
shared colors =["yellow", "red", "green", "blue", "purple", "cyan", "black", "#FF00FE", "gray"]
shared fonts = ["smallest", "gui-1", "gui-2", "gui-3", "gui-4", "gui-5", "gui-6"]
shared face_list = [random_terran(), random_terran_female(),random_terran_male(),random_terran_fluid() ]

shared recruits = ~~[
    "Pat:^^Can helm a ship through the toughest of asteroid belts",
    "Gwen:^^A real sharpshooter",
    "Randle:^^Top notch no nonsense scientist",
    "Steve:^^Just out of Detocs. Barely an engineer"
    ]~~


======= menu  ===============
section style="area:0,5,10,95;row-height:35px;"

button "Text":
    jump show_text
end_button
row

button "Buttons":
    jump show_button
end_button
row
button "Checkbox":
    jump show_checkbox
end_button
row
button "dropdown":
    jump show_dropdown
end_button
row
button "Slider":
    jump show_slider
end_button
row
button "Text Input":
    jump show_text_input
end_button
row
button "Clickable":
    jump show_clickable
end_button
row
button "Custom":
    simulation create
    jump show_custom
end_button
row
button "Table-ish":
    simulation create
    jump show_skills
end_button
row
section style="area:25,20,80,95;"
""""""" Example Gui """""""""""
await gui

========= show_text =======

section style="area:2,2,80,95;"

for i in range(len(fonts)):
    row
    """color:{colors[i]};font:{fonts[i]};text: This is test"""
next i

await choice:
    + "menu":
        -> menu
end_await

========= show_button =======

section style="area:2,20,80,95;"
for i in range(len(fonts)):
    row
    button """color:{colors[i]};font:{fonts[i]};text: This is test""":
    end_button
next i


await choice:
    + "menu":
        -> menu
end_await

========= show_checkbox =======
check = False
section style="area:2,20,80,95;"
for i in range(len(fonts)):
    row
    checkbox check """color:{colors[i]};font:{fonts[i]};text: This is test;""" 
next i


await choice:
    + "menu":
        -> menu
end_await

========= show_dropdown =======
check = "test"
section style="area:2,20,80,95;"


for i in range(len(fonts)):
    row
    dropdown check """color:{colors[i]};font:{fonts[i]};text: This is test;list:Fred,Wilma,Betty,Barney""":
        #-> show_dropdown
    end_dropdown
next i


await choice:
    + "menu":
        -> menu
end_await


========= show_slider =======
check = 0.5
section style="area:2,20,80,95;"


for i in range(len(fonts)):
    row
    slider check """color:{colors[i]};font:{fonts[i]};low:0;high:2; show_number:no"""
    #slider check 0 2 0.5
next i


await choice:
    + "menu":
        -> menu
end_await


========= show_text_input =======
check = "text"
section style="area:2,20,80,95;"


for i in range(len(fonts)):
    row
    input check """color:{colors[i]};font:{fonts[i]}; desc: Description/title"""
next i


await choice:
    + "menu":
        -> menu
end_await


========= show_custom =======

ship_name = ""
ship_cur = None
section style="area: 0, 0, 100,10;"
""" 
This is an example that shows custom gui controls.
Custom controls can be created in python.
"""

section style="area: 1, 10, 45,100;"

row style="row-height: 40px;"
hole
input ship_name "desc: Ship Name;text: "
row style="row-height: 50px;"
button "Add Ship":
    if len(ship_name) >0:
        do player_spawn(0,0,0, ship_name, "tsn", ship_type.value )

        ship_name = ""
        -> show_custom
    end_if
end_button
row style="row-height: 300px;"
#pick ship ship_type

gui control ship_type ship_picker_control("Your Ship", ship_cur)

section style="area: 50, 10, 100,100;"
row style="row-height: 20px;"

"""Player Ships:"""

ships = to_object_list(role("__PLAYER__"))
row style="row-height: 50;"
gui control ship_list list_box_control(ships, text=lambda ship: ship.comms_id, ship=lambda ship: ship.art_id)

await choice:
    + "menu":
        simulation create
        for ship in ships:
            do ship.destroyed()
        next ship
        -> menu
end_await


========= show_clickable =======
check = "text"

for x in range(len(recruits)):

section style="area:10+x*20,20,25+x*20,65;" clickable "color:  blue; text:Recruit!; font:gui-5" data=~~{"pick":x}~~:
    ~~ print(f"Click {pick}") ~~
    # Pick is temp, this promotes it high, this is kinda ugly
    the_pick = pick
    -> show_recruit
end_clickable

face face_list[x]
row
"""color:cyan;font:gui-3; text:{recruits[x]}"""
next x


await choice:
    + "menu":
        -> menu
end_await

========= show_recruit =======

section style="area:2,20,80,95;"
face face_list[the_pick]
""" Ready for duty """

await choice:
    + "menu":
        -> menu
end_await


========= show_skills =======


section style="area:0,0,100,30px;"
########### TABS
for t in skills:
    if t == tab:
        """justify:center; color:black;text:{t} """ style="background: #ffF;padding: 3px,3px,3px,3px;click_text: {t};click_tag: tab_{t};"
    else:
        """justify:center; color:white;text:{t} """ style="background: #0008;padding: 3px,3px,3px,3px;click_text: {t};click_tag: tab_{t};"
    end_if
next x

on click:
    clicked = __CLICKED__.split("_")
    click_type = clicked[0].strip().lower()
    click_value = clicked[1].strip().lower()
    if click_type == "tab":
        if tab != click_value:
            tab = click_value
            jump show_skills
        end_if
    end_if
end_on

deez_category = skills.get("sensors")
deez_tier = deez_category.get("tier")
deez_skills = deez_tier[0].get("skills")

section style="area:10,10,90,90;"
tier_colors = ["#9993", "#0FF3", "#FF03", "#f0f3", ]

row style="background: #0f03;row-height:45px;padding: 0,0,0,3px;"
"""" justify: center; text:{tab} """"
tier = 0
for t in range(4):
    c = {tier_colors[tier]}
    row style="background: {tier_colors[tier]};row-height:15;padding: 0,0,0,3px;"
    for ii in range(4):
        blank
        i_i = random.randrange(120)
        icon "icon_index: {i_i}; color: white;" style="click_font: gui-5;click_color: blue;click_text: SKILL;click_tag: skill_{t}_{ii};"
    next ii
    blank
    tier += 1
next t


await choice:
    + "menu":
        -> menu
end_await
